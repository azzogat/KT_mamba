#version 150

varying vec3 outNormal;
varying vec2 outUV;
varying vec3 outColor;
varying vec3 outPos;

uniform sampler2D grassTex;
uniform sampler2D sandTex;
uniform sampler2D rockTex;

vec3 SampleTriPlanar(sampler2D tex,vec3 normal,vec3 PositionWS) {

	float mXY = abs(normal.z);
	float mXZ = abs(normal.y);
	float mYZ = abs(normal.x);
	
	float scale = 1.0 / 4.0;
	
	float total = mXY + mXZ + mYZ;
	mXY /= total;
	mXZ /= total;
	mYZ /= total;
	
	vec3 cXY = texture2D(tex, PositionWS.xy * scale).xyz;
	vec3 cXZ = texture2D(tex, PositionWS.xz * scale).xyz;
	vec3 cYZ = texture2D(tex, PositionWS.yz * scale).xyz;

	return cXY*mXY + cXZ*mXZ + cYZ*mYZ; 
}

  void main(){
    float slope = 1.0 - abs(dot(normalize(outNormal),vec3(0,1,0)));
	float NdotL = clamp(dot(normalize(outNormal),normalize(vec3(1,1,1))),0.0,1.0);
	vec3 color = vec3(0,0,0);
	vec3 grass = SampleTriPlanar(grassTex,outNormal,outPos);
	vec3 sand = SampleTriPlanar(sandTex,outNormal,outPos);
	vec3 rock = SampleTriPlanar(rockTex,outNormal,outPos);
	float grassHi = 0.1;
	float sandLo = 0.15;
	float sandHi = 0.2;
	float rockLo = 0.4;
	
	if (slope <= grassHi) {
		color = grass;
	}
	if (slope > grassHi && slope <= sandLo) {
		float blend = smoothstep(grassHi,sandLo,slope);
		color += (1.0-blend) * grass;
		color += (blend) * sand;
	}
	if (slope > sandLo && slope <= sandHi) {
		color = sand;
	}
	if (slope > sandHi && slope <= rockLo) {
		float blend = smoothstep(sandHi,rockLo,slope);
		color += (1.0-blend) * sand;
		color += (blend) * rock;
	}
	if(slope > rockLo) color = rock;
	color = color*0.3 + color*NdotL + outColor;
    gl_FragColor = vec4(color,1);
  }